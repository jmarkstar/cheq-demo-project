plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.chrisney.enigma'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.jmarkstar.cheqdemoproj"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {

            //https://dagger.dev/hilt/gradle-setup
            // The Hilt Gradle plugin sets annotation processor arguments.
            // If you are using other libraries that require annotation processor arguments, make sure you are adding arguments instead of overriding them.
            //For example, the following notably uses += to avoid overriding the Hilt arguments.
            annotationProcessorOptions {
                arguments += ["room.incremental" : "true"]
            }
        }

        //As OWASP suggestions, we need to secure the data which are stored in
        // the device. In this case, I'll encrypt the sqlite.db and the shared_preference.xml
        buildConfigField("boolean", "enableEncryption", "true")
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            //v1 signature scheme makes our app vulnerable to Janus vulnerability on Android <7.0
            v1SigningEnabled false
            v2SigningEnabled true
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix = ".DEBUG"
            debuggable true
            minifyEnabled false

            buildConfigField "boolean", "enableEncryption", "false"
        }

        staging {
            minifyEnabled true
            debuggable true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            //As OWASP suggestions, we need to disable the android:debuggable attribute and sign the apk.
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

kapt {
    correctErrorTypes true
}

//https://developer.android.com/training/dependency-injection/hilt-testing
hilt {
    enableTransformForLocalTests = true
}

//As OWASP suggestions, we need to prevent the reverse engineering of the apk. In this case, I'm encrypting a java class
// where I have some secret keys with enigma(I would prefer to use DexGuard) before the obfuscation with Proguard.
enigma {
    enabled = true
    injectFakeKeys = true
    classes = ["com.jmarkstar.cheqdemoproj.common.security.Passphrases.java"]
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //Database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "net.zetetic:android-database-sqlcipher:$android_sqlcipher"
    implementation "androidx.sqlite:sqlite-ktx:$sqlite_ktx_version"

    implementation "androidx.security:security-crypto:$security_crypto_version"

    //DI - Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_viewmodel_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_viewmodel_version"

    //App Logging and crashes
    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation platform("com.google.firebase:firebase-bom:$firebase_BoM")
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // For local unit tests
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation 'junit:junit:4.13.1'
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    testAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"

    // For instrumentation tests
    androidTestImplementation  "com.google.dagger:hilt-android-testing:$hilt_version"
    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}